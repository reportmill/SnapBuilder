import java.text.SimpleDateFormat

plugins {
    id 'application'
}

group 'com.reportmill'
version new SimpleDateFormat("yyyy.MM").format(new Date())

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
}

dependencies {

    // SnapKit project
    implementation project(':SnapKit')
}

mainClassName = 'snapbuild.app.App'

/**
 * Build executable jar SnapBuilderAll.jar.
 *
 * We could use the standard jar task just by using jar { ... } and deleting 'baseName' and 'with jar' lines.
 */
task buildFullJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'snapbuild.app.App',
        'Application-Name': 'SnapBuilder'
    }
    archiveBaseName = 'SnapBuilderAll'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    dependsOn build
}