
plugins {
    id 'java'
    id 'application'
    id 'idea'
}

group 'com.reportmill'
version '1.0.0'

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

repositories {

    // Jitpack makes builds from GitHub repos
    maven { url 'https://jitpack.io' }
}

dependencies {

    // If Local SnapKit project, use that
    if (findProject(':SnapKit') != null) {
        println 'Using local SnapKit project'
        implementation project(':SnapKit')
    }

    // Otherwise use Jitpack.io to generate maven package from Github SnapKit repo, latest commit
    else {
        println 'Using latest SnapKit GitHub repo'
        implementation 'com.github.reportmill:SnapKit:master-SNAPSHOT'
    }
}

mainClassName = 'snapbuild.app.App'

/**
 * Build executable jar SnapBuilderAll.jar.
 *
 * We could use the standard jar task just by using jar { ... } and deleting 'baseName' and 'with jar' lines.
 */
task buildExecJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'snapbuild.app.App'
    }
    archiveBaseName = 'SnapBuilderAll'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}